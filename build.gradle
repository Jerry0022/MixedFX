// TODO @Maxi How to build all subprojects / modules into one MixedFX jar?
// Trying to build this there is a jar containing almost nothing but not the modules.
// Is this necessary, anyway? If not please delete the last lines of this script

/*
 * SCRIPT APPLIED TO ROOT PROJECT AND ALL SUBPROJECTS
 */

allprojects {
    group = 'de.lantool'

    apply plugin: 'java' // TODO @Maxi Shift this to subprojects task or is this plugin needed for root project?
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'maven'

    processResources {
        from('src') {
            include '**/*.css'
            include '**/*.xml'
            include '**/*.bin'
            include '**/*.ahk'
            include '**/*.exe'
        }

    }

    // BUILD AND INSTALL/UPLOAD

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "file:${System.env.GDRIVE}\\LanTool\\mvn-release")
            }
        }
    }
}

/*
 * SCRIPT APPLIED TO ALL SUBPROJECTS
 */

subprojects {
    repositories {
        /*
         * Common repositories
         */
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "file:${System.env.GDRIVE}\\LanTool\\mvn-release" }
        /*
         * For de.mixedfx.texttospeech
         */
        maven {
            url "http://dl.bintray.com/marytts/marytts"
        }
        /*
         * For de.mixedfx.texttospeech
         */
        maven { url "http://dl.bintray.com/dfki-lt/maven" }
    }

    dependencies {
        // In IDE Annotation Processor must be enabled
        compile 'org.projectlombok:lombok:1.16.6'
        // E. g. for simple CDI via Spring
        compile 'org.springframework:spring-context:4.2.1.RELEASE'
    }
}

/*
 * SCRIPT APPLIED ONLY TO ROOT PROJECT
 */

version = '1.3'

/*
 * DEPENDENCIES
 */

dependencies {
    // Project dependencies
    compile project(":mixedfx-cdi2jfx")
    compile project(":mixedfx-gui")
    compile project(":mixedfx-network")
    compile project(":mixedfx-textandspeech")
    compile project(":mixedfx-tools")
    compile project(":mixedfx-ts3")
    compile project(":mixedfx-windows")
}

jar {
    manifest {
        attributes 'Implementation-Title': 'MixedFX',
                'Implementation-Version': version
    }
}